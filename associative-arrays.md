# Ассоциативные массивы

В языке D есть встроенные *ассоциативные массивы*, также известные как
хэш-таблицы. Ассоциативный массив с ключом типа `string` и значением типа `int`
объявляется так:

    int[string] arr;

Доступ к значению можно получить по его ключу и установить таким образом:

    arr["key1"] = 10;

Чтобы проверить, существует ли ключ в ассоциативном массиве, можно использовать выражение `in`:

    if ("key1" in arr)
        writeln("Yes");

Выражение `in` возвращает указатель на значение, если ключ может быть найден, либо `null` в противном случае. Такую проверку существования и запись значения можно легко объединить:

    if (auto val = "key1" in arr)
        *val = 20;

Доступ к несуществующему ключу приведёт к ошибке `RangeError`, что немедленно
прервёт выполнение приложения. Для безопасного доступа с заданными по умолчанию
значениями можно использовать `get(key, defaultValue)`.

У ассоциативных массивов, как и у обычных массивов, есть свойство `.length`, а также метод `.remove(val)` для удаления записи по её ключу. В качестве упражнения читателю предлагается самостоятельно исследовать специальные диапазоны `.byKeys`
и `.byValues`.

### Подробнее

- [Ассоциативные массивы в _Programming in D_](http://ddili.org/ders/d.en/aa.html)
- [Спецификация на ассоциативные массивы](https://dlang.org/spec/hash-map.html)
- [std.array.byPair](http://dlang.org/phobos/std_array.html#.byPair)

## {SourceCode}

```d
import std.stdio;

/**
Разбивает заданный текст на слова и возвращает
ассоциативный массив, в котором слова сопоставлены 
с их количеством (показывает, сколько раз слово 
встречается в тексте)

Параметры:
    text = текст для разделения
*/
int[string] wordCount(string text)
{
    // Функция splitter лениво разбивает
    // исходный текст, возвращая диапазон
    import std.algorithm.iteration: splitter;
    import std.string: toLower;

    // Проиндексировано по словам и возвращает
    // количество
    int[string] words;

    foreach(word; splitter(text.toLower(), " "))
    {
        // Инкремент счётчика, если слово было найдено. 
        // Значение по умолчанию для счётчика
        // равно 0.
        words[word]++;
    }

    return words;
}

void main()
{
    string text = "D is a lot of fun";

    writeln("Word counts: ", wordCount(text));
}
```
